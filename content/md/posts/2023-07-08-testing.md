{:title "Starting out with testing"
 :layout :post
 :tags  ["cljs" "testing" "re-frame"]
 :toc true}

## Overview

This article covers why I want to have testing in my application, how to do the setup for the type of tests I'll be doing, and what future testing posts will be about.

This post will discuss what is needed beyond the setup procedure described in "setting up the project part 1" and "setting up the project part two" create the tests. Please follow the steps in the previous posts before continuing on with this post.

## Why do I want testing in my application?

For my application, I want to know when it is working. Instead of having to wait for something bad to happen so I learn that something has gone wrong, it would be nice to check beforehand so I can then diagnose the problem.

I started searching online for ways to accomplish this, and testing was the first thing that showed up. From what I read, testing is used to check a variety of things and acts as an alarm of sorts. The test fails, and from there you can investigate the problem.

The three types of tests which I saw were unit, integration, and acceptance testing.

### Unit tests

Unit tests take a function and checks the result of the function given a specific input. It focuses mainly on testing that one component, and only that component, works well.

For example, in my project I have to setup the database when starting everything up. 
I have a function below that returns a map with the default values of the database

```clojure
(re-frame/reg-event-db
 ::initialize-db
 (fn-traced [_ _]
            db/default-db))
```


I want to test that this function returns the correct default database. To do this I made a file in test/haggadah called events_test.cljs. In the namespace I added a few dependencies for the testing framework I use and to the file where I store my events. It should look like this.

```clojure
(ns haggadah.events-test
  (:require [haggadah.subs :as subs]
            [cljs.test :as t :include-macros true]
            [re-frame.core :as rf]
            [day8.re-frame.test :as rf-test]
            [haggadah.events :as events]
            [haggadah.core :as core]
            [haggadah.routes :as routes]))
```

For testing that `::initialize-db` returns default-db, which is just a map of the form `{:name nil :uid nil :user :unloaded}`,
I want to initialize the database and then check that the values of name, uid, and user correspond to their correct values. The test
below accomplishes this.

```clojure 
(t/deftest admin-login
  (rf-test/run-test-sync
   (rf/dispatch [::events/initialize-db])
   (let [name (rf/subscribe [::subs/name])
         uid (rf/subscribe [::subs/uid])
         user (rf/subscribe [::subs/user])]
     (t/are [x y] (= x y)
       "(Unknown)" @name
       nil @uid
       :unloaded @user))))
```

### Integration testing
 
Integration testing is about checking to see that multiple services/components work together.

For example, in my application I use firebase, an application development program which gives me a database and authentication scheme (among other things). If I have a test to see that the user's account information is updated after logging in with correct credentials, I need to be sure that my application is communicating properly with firebase so that my user is getting authenticated. 

To setup this test I need to initialize firebase and the routing for my application beforehand. 

### Initialize firebase

For information about how to setup firebase, look in the post called "Setting up the project part 2"

### Initialize routing

For information about how to setup routing, look in the post called "Setting up the project part 1"

We can now write our test for registering a user. If the user is registered and logs in, then their account information should be updated to reflect them being the current user. 

```clojure
(t/deftest registered-user-login
  (rf-test/run-test-async
   (routes/init-routes!)
   (core/firebase-init!)
   (rf/dispatch-sync [::events/initialize-db])
   (rf/dispatch [::events/login])
   (rf-test/wait-for [::events/fetch-user] 
    (let [user (rf/subscribe [::subs/user])
          name (rf/subscribe [::subs/name])]
      (t/are [x y] (= x y)
        :registered @user
        "han@skywalker.com" @name)))))
```

First, I initialize my routes, setup the database, and connect to firebase. After that, I login and wait for the user's information to be fetched. I then check whether the name of the user matches the credentials entered earlier and if the user is noted as registered.

## Acceptance testing

Acceptance testing is a bit different from the first two tests, as it tests things from the perspective of the user. You think of what the user would need to do to in order to do something, such as logging in, and you want to test whether what they see matches what should be there.

For example, in my application when the user logs in they are taken to their dashboard and shown a welcome message. I want to see that when they reach the dashboard, they see the welcome message. To make this test, we need to set a few things up.

Setup the directory for acceptance testing as directed in the post "Setting up the project part 2". After, create a file in test/acceptance called admin_login.clj. Here, we are going to put the test that we described above.

Change the namespace so it appears like this. 
```clojure
(ns acceptance.admin-login-test
  (:require  [clojure.test :as t]
             [etaoin.api :as e]
             [etaoin.keys :as k]
             [acceptance.core :as c :refer [driver]])
  (:import com.google.firebase.cloud.FirestoreClient))
```

Then, add the following code to the file.

```clojure
(def default-message
  "Hello (Unknown). We're glad to see you.")

(def admin-login-message
  "Hello han@skywalker.com. Welcome to your dashboard. To make a new document, click the button to your right. To share and edit your existing documents, look at your documents below")

(t/use-fixtures :once c/init-firebase) ;; Sets up firebase once for each test
(t/use-fixtures :each c/with-screenshot) ;; Takes a screenshot at the end of every test

(t/deftest message-test
  (t/testing "When the admin user exists"
    (doto driver
      (c/home->dashboard)
      (e/wait-has-text-everywhere admin-login-message))
    (let [actual (e/get-element-text driver {:data-testid :user})]
      (e/screenshot driver "screenshots/message-test-when-the-admin-exists.png")
      (t/is (= admin-login-message actual)))))
```

In this test, I login and head to the dashboard, and then see if the welcome message has appeared. If it does not appear, an error is thrown. If it is found, I check to see if the welcome message matches the admin login message.



## Future posts

For future posts about testing, I will talk about new situations I had to deal with in regards to testing, or my thoughts on how tests could be made easier to understand. I will also include more posts which focus on a specific type of test, such as unit/acceptance/integration, and expand on my experience with that type of test so far.




